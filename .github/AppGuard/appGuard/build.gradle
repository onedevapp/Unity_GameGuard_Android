plugins {
    alias(libs.plugins.android.library)
}
ext {
    PUBLISH_GROUP_ID = 'com.onedevapp'
    PUBLISH_ARTIFACT_ID = 'appguard'
    PUBLISH_VERSION = '1.2'
}

android {
    namespace 'com.onedevapp.appguard'
    compileSdk 34

    defaultConfig {
        minSdk 21

        versionCode 2
        versionName "1.2"

        libraryVariants.all { variant ->
            def variantName = variant.name.capitalize()
            def copyTaskName = "copy${variantName}Artifacts"
            def assembleTaskName = "assemble${variantName}"
            task(copyTaskName, type: Copy, dependsOn: assembleTaskName, group: "build") {
                variant.outputs.each { output ->
                    def newOutputName = output.outputFile.name.replace(".aar", "-" + android.defaultConfig.versionName + ".aar")
                    from(output.outputFile.parent) {
                        include output.outputFile.name
                        rename output.outputFile.name, newOutputName
                    }
                }
                //into project.buildDir
                into(layout.buildDirectory.dir("../../release"))
            }
            project.tasks.getByName(assembleTaskName).finalizedBy(copyTaskName)
        }
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    compileOnly project(":aarLibs")
}